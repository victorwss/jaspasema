plugins {
    id("com.github.spotbugs") version "4.7.5"
    id "java-library"
    id "maven-publish"
    id "checkstyle"
    id "signing"
    id "jacoco"
    id "pmd"
}

allprojects {
    group = "ninja.javahacker.jaspasema"
    version = "0.7.0-2021.09.20"
}

def versionJavaCompiler = "10"
def doclint = true

def versionApiguardian = "1.1.1"
def versionCheckstyle = "9.0"
def versionFindSecBugs = "1.11.0"
def versionHibernate = "5.5.7.Final" //"6.0.0.Alpha9"
def versionJackson = "2.12.5"
def versionJaCoCo = "0.8.7"
def versionJavalin = "3.8.0"         //"4.0.1"
def versionJaxb = "2.4.0-b180830.0359"
def versionJakartaJpa = "2.2.3"      //"3.0.0"
def versionJcip = "1.0"
def versionJpaSimpleTransactions = "1.0.0-2021.09.20"
def versionJsr305 = "3.0.2"
def versionJunit = "5.8.0"
def versionLombok = "edge-SNAPSHOT"  //"1.18.22"
def versionMocker = "1.1.0-2021.09.20"
def versionMySql = "8.0.26"
def versionPmd = "6.38.0"
def versionReflections = "0.9.12"
def versionReifiedGenerics = "1.1.0-2021.09.20"
def versionSbContrib = "7.4.7"
def versionServlet = "3.1.0"
def versionSpotBugs = "4.4.1"
def versionSlf4j = "1.8.0-beta4"     //"2.0.0-alpha5"

// Do not change anything below here.

println("Using Java " + org.gradle.api.JavaVersion.current() + ".")

allprojects {
    configurations {
        scm
    }

    repositories {
        flatDir {
            dirs("libs")
        }
        mavenLocal()
        mavenCentral()
        maven {
            url("https://projectlombok.org/edge-releases")
        }
    }
}

subprojects {
    apply(plugin: "com.github.spotbugs")
    apply(plugin: "java-library")
    apply(plugin: "maven-publish")
    apply(plugin: "checkstyle")
    apply(plugin: "signing")
    apply(plugin: "jacoco")
    apply(plugin: "pmd")

    dependencies {

        // Jackson
        api(group: "com.fasterxml.jackson.core", name: "jackson-core", version: versionJackson)
        api(group: "com.fasterxml.jackson.core", name: "jackson-databind", version: versionJackson)
        api(group: "com.fasterxml.jackson.core", name: "jackson-annotations", version: versionJackson)
        api(group: "com.fasterxml.jackson.module", name: "jackson-module-parameter-names", version: versionJackson)
        api(group: "com.fasterxml.jackson.datatype", name: "jackson-datatype-jsr310", version: versionJackson)
        api(group: "com.fasterxml.jackson.datatype", name: "jackson-datatype-jdk8", version: versionJackson)

        // Javalin
        api(group: "io.javalin", name: "javalin", version: versionJavalin)

        // JUnit
        testImplementation(group: "org.junit.jupiter", name: "junit-jupiter-api", version: versionJunit)
        testImplementation(group: "org.junit.jupiter", name: "junit-jupiter-params", version: versionJunit)
        testRuntimeOnly(group: "org.junit.jupiter", name: "junit-jupiter-engine", version: versionJunit)
        testCompileOnly(group: "org.apiguardian", name: "apiguardian-api", version: versionApiguardian)

        // Lombok
        annotationProcessor(group: "org.projectlombok", name: "lombok", version: versionLombok)
        compileOnlyApi(group: "org.projectlombok", name: "lombok", version: versionLombok)
        testAnnotationProcessor(group: "org.projectlombok", name: "lombok", version: versionLombok)
        testCompileOnly(group: "org.projectlombok", name: "lombok", version: versionLombok)

        // Reified-Generics
        api(group: "ninja.javahacker", name: "reified-generic", version: versionReifiedGenerics)

        // SpotBugs + plugins
        spotbugs(group: "com.github.spotbugs", name: "spotbugs", version: versionSpotBugs)
        spotbugs(group: "org.slf4j", name: "slf4j-api", version: versionSlf4j)
        spotbugs(group: "org.slf4j", name: "slf4j-simple", version: versionSlf4j)
        spotbugs(configurations.spotbugsPlugins.dependencies)
        compileOnlyApi(group: "net.jcip", name: "jcip-annotations", version: versionJcip)
        compileOnlyApi(group: "com.github.spotbugs", name: "spotbugs-annotations", version: versionSpotBugs)
        testCompileOnly(group: "net.jcip", name: "jcip-annotations", version: versionJcip)
        testCompileOnly(group: "com.github.spotbugs", name: "spotbugs-annotations", version: versionSpotBugs)
        spotbugsPlugins(group: "com.h3xstream.findsecbugs", name: "findsecbugs-plugin", version: versionFindSecBugs)
        spotbugsPlugins(group: "com.mebigfatguy.sb-contrib", name: "sb-contrib", version: versionSbContrib)
    }

    configurations.compileOnlyApi.setCanBeResolved(true)
    configurations.implementation.setCanBeResolved(true)
    configurations.testCompileOnly.setCanBeResolved(true)
    configurations.testImplementation.setCanBeResolved(true)

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.debug = true
        options.fork = true
        options.compilerArgs << "-parameters" << "-Xlint:all,-processing,-requires-transitive-automatic,-requires-automatic,-missing-explicit-ctor" << "-Xmaxwarns" << "1000" << "-Xmaxerrs" << "1000"
    }

    compileJava {
        sourceCompatibility = versionJavaCompiler
        targetCompatibility = versionJavaCompiler
        if (doclint) {
            options.compilerArgs << "-Xdoclint:all/protected"
        } else {
            options.compilerArgs << "-Xdoclint:none"
        }
        doFirst {
            //println("\nClasspath + Modulepath compileOnlyApi:;${configurations.compileOnlyApi.asPath}".replaceAll(";+", ";").replace(";", "\n   "))
            //println("\nClasspath + Modulepath implementation:;${configurations.implementation.asPath}".replaceAll(";+", ";").replace(";", "\n   "))
            options.compilerArgs += [
                "--module-path", "${configurations.compileOnlyApi.asPath};${configurations.implementation.asPath}".replaceAll(";+", ";")
            ]
            classpath = files()
        }
    }

    task delombok {
        description = "Delomboks the source code."
        doLast {
            def delombokPath = "${configurations.compileOnlyApi.asPath};${configurations.implementation.asPath}"
            ant.mkdir(dir: "build/src-delomboked")
            ant.taskdef(name: "delombok", classname: "lombok.delombok.ant.Tasks\$Delombok", classpath: delombokPath)
            ant.delombok(verbose: "false", encoding: "UTF-8", to: "build/src-delomboked", from: "src/main/java", modulepath: delombokPath)
        }
    }
    tasks.withType(Javadoc) {
        dependsOn(delombok)
        source = ["build/src-delomboked"]
        options.encoding = "UTF-8"
        options.charSet = "UTF-8"
        options.docEncoding = "UTF-8"
        options.docTitle = "${project.name} API"
        options.windowTitle = "${project.name} API"
        options.header = "<b>${project.name}</b>"
        options.addBooleanOption("html5", true)
        options.tags = ["apiNote:a:API Note:", "implSpec:a:Implementation Requirements:", "implNote:a:Implementation Note:"]
        if (doclint) {
            //options.addBooleanOption("Xdoclint:all/protected", true)
        } else {
            options.addBooleanOption("Xdoclint:none", true)
        }
        options.addStringOption("Xmaxerrs", "1000")
        options.addStringOption("Xmaxwarns", "1000")
        doFirst {
            options.modulePath += classpath
            classpath = files()
        }
    }

    checkstyleMain {
        configFile = rootProject.file("${rootDir}/config/checkstyle/main.xml")
    }

    checkstyleTest {
        configFile = rootProject.file("${rootDir}/config/checkstyle/test.xml")
    }

    checkstyle {
        toolVersion = versionCheckstyle
        configProperties = [
            "checkstyle.cache.file": "${buildDir}/checkstyle.cache",
        ]
        ignoreFailures = true
        showViolations = true
    }

    pmd {
        toolVersion = versionPmd
        ignoreFailures = true
    }

    spotbugs {
        toolVersion = versionSpotBugs
        effort = "max"
        reportLevel = "low"
        omitVisitors = ["WeakExceptionMessaging", "OverlyPermissiveMethod"]
        ignoreFailures = true
    }

    tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
        reports {
            xml.required = false
            html.required = true
        }
    }

    jar {
        duplicatesStrategy = "exclude"
    }

    task sourcesJar(type: Jar) {
        from(sourceSets.main.allSource)
        classifier("sources")
    }

    task javadocJar(type: Jar) {
        from(javadoc)
        classifier("javadoc")
    }

    test {
        useJUnitPlatform()
        finalizedBy(jacocoTestReport)
        ignoreFailures = true
    }

    jacoco {
        toolVersion = versionJaCoCo
    }

    jacocoTestReport {
        dependsOn(test)
        reports {
            xml.required = false
            csv.required = false
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from(components.java)
                artifact(sourcesJar)
                artifact(javadocJar)
                pom {
                    developers {
                        developer {
                            name = "Victor Williams Stafusa da Silva"
                            email = "victorwssilva@gmail.com"
                        }
                    }
                }
            }
        }
        repositories {
            mavenLocal()
        }
    }

    spotbugsTest.enabled = false
    pmdTest.enabled = false
}

task mergedJavadoc(type: Javadoc) {
    description = "Creates Javadoc from all the projects."
    title = "All modules"
    destinationDir = new File(project.buildDir, "merged-javadoc")

    // Note: The closures below are executed lazily.
    source {
       subprojects*.sourceSets*.main*.allSource
    }
    classpath.from {
        subprojects*.configurations*.compile*.copyRecursive({ !(it instanceof ProjectDependency); })*.resolve()
    }
}

project(":core") {
    def moduleName = "ninja.javahacker.jaspasema.core"
    description = "Jaspasema Core"
    jar {
        archiveBaseName = "jaspasema-core"
        inputs.property("moduleName", moduleName)
    }
}

project(":app") {
    def moduleName = "ninja.javahacker.jaspasema.app"
    description = "Jaspasema App"
    jar {
        archiveBaseName = "jaspasema-app"
        inputs.property("moduleName", moduleName)
    }
    dependencies {
        api(project(":core"))

        // Reflections
        implementation(group: "org.reflections", name: "reflections", version: versionReflections)

        // JPA
        api(group: "jakarta.persistence", name: "jakarta.persistence-api", version: versionJakartaJpa)

        // JPA Simple Transactions
        api(group: "ninja.javahacker.jpasimpletransactions", name: "core", version: versionJpaSimpleTransactions)
    }
    compileJava {
        dependsOn(":core:jar")
    }
}

project(":sampleproject") {
    apply(plugin: "application")
    def moduleName = "ninja.javahacker.jaspasema.sampleproject"
    description = "Jaspasema Sample Project"
    mainClassName = "ninja.javahacker.jaspasema.sampleproject.SampleProject"
    jar {
        archiveBaseName = "jaspasema-sampleproject"
        inputs.property("moduleName", moduleName)
    }
    dependencies {
        implementation(project(":app"))

        // MySQL / MariaDB
        implementation(group: "mysql", name: "mysql-connector-java", version: versionMySql)

        // Hibernate
        implementation(group: "org.hibernate", name: "hibernate-core", version: versionHibernate) {
            exclude(group: "javax.persistence", module: "javax.persistence-api")
        }

        // JPA Simple Transactions
        api(group: "ninja.javahacker.jpasimpletransactions", name: "core", version: versionJpaSimpleTransactions)
        api(group: "ninja.javahacker.jpasimpletransactions", name: "hibernate", version: versionJpaSimpleTransactions)

        // Servlet
        implementation(group: "javax.servlet", name: "javax.servlet-api", version: versionServlet)
    }
    compileJava {
        dependsOn(":app:jar")
    }
}